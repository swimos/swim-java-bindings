import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

ext.nativeProperties = new Properties();
ext.nativeProperties.load(file(project.projectDir.absolutePath + "/native.properties").newReader())

ext {
    ext.moduleName = 'swim.client'

    bridge_dir = "../../rust-java-bridge"
    target_dir = bridge_dir.concat('/target')

    if (DefaultNativePlatform.currentOperatingSystem.isMacOsX()) {
        target_dir = target_dir.concat('/x86_64-apple-darwin')
    }

    debug_dir = target_dir.concat('/debug')
    release_dir = target_dir.concat('/release')
}

test {
    def library_path = '-Djava.library.path='.concat(release_dir)
    jvmArgs library_path
    useJUnitPlatform()

    testLogging.showStandardStreams = true
}

def compileRust(release) {
    exec {
        println "Compiling rust libraries (".concat(release ? "release)" : "debug)")

        workingDir(bridge_dir)
        environment('RUSTFLAGS', '-Awarnings')

        def args = ['cargo', 'build']

        if (DefaultNativePlatform.currentOperatingSystem.isMacOsX()) {
            args.add('--target')
            args.add('x86_64-apple-darwin')
        }

        def features = []
        if (ext.nativeProperties.tls.toBoolean()) {
            features.add('tls')
        }
        if (ext.nativeProperties.deflate.toBoolean()) {
            features.add('deflate')
        }

        if (!features.isEmpty()) {
            args.add('--features')
            args.addAll(features.join(','))
        }

        if (release) {
            args.add('--release')
        }

        commandLine args
    }
}

def compileRustDebug = tasks.register('compileRustDebug') {
    compileRust(false)
}

compileTestJava {
    doLast {
        compileRust(false)
    }
}

assemble {
    doLast {
        compileRust(true)
    }
}

check.dependsOn compileRustDebug

tasks.register('cleanRust') {
    doLast {
        exec {
            println "cleaning"
            workingDir(bridge_dir)
            commandLine 'cargo', 'clean'
        }
    }
}

application {
    def library_path = '-Djava.library.path='.concat(release_dir)
    applicationDefaultJvmArgs = [library_path]
}

tasks.create(name: "cleanLibs", type: Exec, description: "Cleaning library directories", dependsOn: "clean") {
    doLast {
        exec {
            workingDir("../../../rust-java-bridge/")
            commandLine 'cargo', 'clean'
        }
    }
}

tasks.create(name: "nativeLibs", type: Sync) {
    from "${project.ext.release_dir}"
    // todo: prefix search by project. I.e, the client project doesn't need to copy the server libraries which it does at present
    include "*.dylib", "*.so", "*.dll"
    into "libs"
}

tasks.named("processResources").configure { dependsOn("nativeLibs") }
tasks.withType(JavaCompile).configureEach {
    compileTask -> compileTask.dependsOn "nativeLibs"
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
            srcDir "${buildDir}/generated/main/java"
        }
        resources {
            srcDir 'libs'
        }
    }
    test {
        java {
            srcDir 'src/test/java'
        }
    }
}
